################################################################################
#                                                                              #
# This file is part of Mold project.                                           #
# Copyright (c) 2015, 2018 Sylwester Wysocki (sw143@wp.pl).                    #
#                                                                              #
# The Mold code and any derived work however based on this software are        #
# copyright of Sylwester Wysocki. Redistribution and use of the present        #
# software is allowed according to terms specified in the file LICENSE         #
# which comes in the source distribution.                                      #
#                                                                              #
# All rights reserved.                                                         #
#                                                                              #
################################################################################

import 'AST.mold'
import 'ParserMold.mold'
import 'ASTTranslator.mold'
import 'ASTTranslatorMIR0.mold'
import 'ASTTranslatorMIR1.mold'

import 'MIRQuadCollector.mold'
import 'MIRQuadTypeGuess.mold'
import 'MIRQuadTypeConvert.mold'
import 'MIRQuadOptimizer.mold'

import 'CodeGeneratorX64.mold'
import 'VMachineStack.mold'

# ------------------------------------------------------------------------------
#                              Helper functions
# ------------------------------------------------------------------------------

function BadUsageError()
  print 'Usage:'
  print '  mold [-c] [--asm] [--mir0] [--mir] <input.mold>'
  exit()
endfunction

# ------------------------------------------------------------------------------
#                                Entry point
# ------------------------------------------------------------------------------

#
# Parse command line parameters.
#

mode = 'interpreter'
path = ''
optimizationLevel = 0

for idx in 1 .. argc
  if argv[idx] is '-c'
    # Compiler mode
    # mold -c <file.mold>
    mode = 'compiler'
    print 'Error: compiler mode not implemented yet.'
    print 'Try --asm option and assemble result by own instead.'
    exit()

  elif argv[idx] is '--asm'
    # Generate assembler
    mode = 'asm'

  elif argv[idx] is '--mir0'
    # Generate assembler
    mode = 'mir0'

  elif (argv[idx] is '--mir') or (argv[idx] is '--mir1')
    # Generate assembler
    mode = 'mir1'

  elif argv[idx] is '--ast'
    # Show AST tree
    mode = 'ast'

  elif argv[idx] is '--astjson'
    # Show AST tree as JSON.
    mode = 'astjson'

  elif argv[idx] is '--debug'
    # Enable debug logs
    UNICC_DEBUG = true

  elif argv[idx] is '-O0'
    optimizationLevel = 0

  elif argv[idx] is '-O1'
    optimizationLevel = 1

  elif argv[idx] is '-O2'
    optimizationLevel = 2

  elif argv[idx] is '-O3'
    optimizationLevel = 3

  elif argv[idx][0] is '-'
    # Unknown -x switch.
    BadUsageError()

  else
    # Unkown option - treat as filename.
    path = argv[idx]
  endif
endfor

if path is ''
  BadUsageError()
endif

#
# Load input file and build Abstract Syntax Tree (AST)
#

parser = new Parser()
ast    = parser.parseFile(path)

#
# Dispatch working mode.
#

if mode is 'ast'
  # Print AST only.
  ast.print()

elif mode is 'astjson'
  # Print AST, but as importable JSON source.
  ast = ast.convert()
  print ast

else
  # Convert AST to form understood by translator.
  # TODO: Remove this step.
  ast = ast.convert()

  if (mode is 'mir0') or (mode is 'interpreter')
    # Translate program to MIR0 (stack based).
    astTranslator = new ASTTranslatorMIR0()
    astTranslator.run(ast)
    code = astTranslator.code

    # Load MIR0 code into virtual machine.
    vm = new VMachineStack()
    vm.loadCode(code)

    if mode is 'mir0'
      # Dump stack MIR0 only.
      vm.dumpCode()

    elif mode is 'interpreter'
      # Run virtual machine (stack based).
      vm.run()
    endif

  else
    # Prepare quads process toolchain.
    # [AST] -> [MIR1] -> [Optimizer] -> [Collector]
    # Translate program to MIR1 (register based).
    astTranslator    = new ASTTranslatorMIR1()
    mirQuadCollector = new MIRQuadCollector()

    if optimizationLevel is 0
      # No optimization - pass raw MIR1 to code generator.
      astTranslator.connectTo(mirQuadCollector)

    elif optimizationLevel is 1
      # Optimization level 1:
      # - Remove redundant load/store
      # - Use machine types if possible.
      mirQuadOptimizer   = new MIRQuadOptimizer()
      mirQuadTypeGuess   = new MIRQuadTypeGuess()
      mirQuadTypeConvert = new MIRQuadTypeConvert()

      astTranslator.connectTo(mirQuadOptimizer)

      mirQuadOptimizer.connectTo(mirQuadTypeGuess).connectTo(mirQuadTypeConvert).connectTo(mirQuadCollector)

    else
      die('error: optimization level #' + str(optimizationLevel) + ' is not implemented yet')
    endif

    # Run build quads process.
    astTranslator.run(ast)

    if optimizationLevel >= 1
      # TODO: Auto flush on stream end.
      mirQuadTypeGuess.flush()
    endif

    if mode is 'mir1'
      # Print MIR quads only
      mirQuadCollector.printQuads()

    elif mode is 'asm'
      # TODO: Better way to pass quads + meta.
      code = astTranslator.code
      code.quads = mirQuadCollector.getQuads()

      # Generate x64 code.
      codeGenerator = new CodeGeneratorX64()
      codeGenerator.run(code)

    else
      BadUsageError()
    endif
  endif
endif
