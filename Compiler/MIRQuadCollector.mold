################################################################################
#                                                                              #
# This file is part of Mold project.                                           #
# Copyright (c) 2015, 2018 Sylwester Wysocki (sw143@wp.pl).                    #
#                                                                              #
# The Mold code and any derived work however based on this software are        #
# copyright of Sylwester Wysocki. Redistribution and use of the present        #
# software is allowed according to terms specified in the file LICENSE         #
# which comes in the source distribution.                                      #
#                                                                              #
# All rights reserved.                                                         #
#                                                                              #
################################################################################

import 'PipeAbstract.mold'

class MIRQuadCollector extends PipeAbstract

  # ----------------------------------------------------------------------------
  #                         Init functions (constructor)
  # ----------------------------------------------------------------------------

  method constructor()
    this.quads = []
  endmethod

  # ----------------------------------------------------------------------------
  #                        Internal helpers (private)
  # ----------------------------------------------------------------------------

  method _pad(item, desiredLen) -> rv
    if typeof(item) is 'undefined'
      # Undefined (probably unused) operand.
      # Show nothing.
      item = '-'
    endif

    if typeof(item) is 'array'
      # Operand is an array.
      # Render item-by-item.
      # TODO: Handle complex data in built-in str().
      rv  = '['
      sep = ''

      for idx in 0 .. len(item)
        rv  = rv + sep + str(item[idx])
        sep = ', '
      endfor
      rv = rv + ']'

    else
      # Default way.
      rv      = str(item)
      padCnt  = desiredLen - len(rv)

      for idx in 0 .. padCnt
        rv = rv + ' '
      endfor
    endif
  endmethod

  # ----------------------------------------------------------------------------
  #                                  Public API
  # ----------------------------------------------------------------------------

  method processOneItem(item)
    cnt             = len(this.quads)
    this.quads[cnt] = item
  endmethod

  method getQuads() -> rv
    rv = this.quads
  endmethod

  method _printSymbolInfoRow(name, readHits, writeHits, type)
    name      = this._pad(name, 10)
    readHits  = this._pad(readHits, 4)
    writeHits = this._pad(writeHits, 4)
    type      = this._pad(type, 8)
    print '    ;', name, readHits, writeHits, type
  endmethod

  method _dumpFrameInfo(frameInfo)
    global SYMBOL_KIND_AS_TEXT

    symbolsIndex = frameInfo['_index']
    symbolsCnt   = len(symbolsIndex)

    this._printSymbolInfoRow('var', 'R', 'W', 'type')
    this._printSymbolInfoRow('----------', '----', '----', '--------')

    for idx in 0 .. symbolsCnt
      item = symbolsIndex[idx]
      this._printSymbolInfoRow(item.name, item.readHits, item.writeHits, item.type)
    endfor
  endmethod

  method printQuads()
    code    = this.quads
    codeCnt = len(code)

    for idx in 0 .. codeCnt
      item    = code[idx]
      opcode  = item[0]

      if opcode is 'proc'
        print 'proc', item[1]['name']
        #this._dumpFrameInfo(item[1]['symbols'])

      elif opcode is 'init_frame'
        print '    init_frame'

      elif opcode is 'endp'
        print 'endp'

      elif opcode is 'comment'
        print ';', item[1]

      elif opcode is 'label'
        print item[1] + ':'

      else
        dst = item[1]
        x   = item[2]
        y   = item[3]

        paddedOpcode = this._pad(opcode, 28)
        paddedDst    = this._pad(dst, 8)
        paddedX      = this._pad(x, 8)
        paddedY      = this._pad(y, 8)

        print '   ', paddedOpcode, paddedDst, paddedX, paddedY
      endif
    endfor
  endmethod
endclass
