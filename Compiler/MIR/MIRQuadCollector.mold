################################################################################
#                                                                              #
# This file is part of Mold project.                                           #
# Copyright (c) 2015, 2018 Sylwester Wysocki (sw143@wp.pl).                    #
#                                                                              #
# The Mold code and any derived work however based on this software are        #
# copyright of Sylwester Wysocki. Redistribution and use of the present        #
# software is allowed according to terms specified in the file LICENSE         #
# which comes in the source distribution.                                      #
#                                                                              #
# All rights reserved.                                                         #
#                                                                              #
################################################################################

import 'Utils/PipeAbstract.mold'

class MIRQuadCollector extends PipeAbstract

  # ----------------------------------------------------------------------------
  #                         Init functions (constructor)
  # ----------------------------------------------------------------------------

  method constructor()
    this.quads        = []
    this.currentFrame = {'symbols': {}}
  endmethod

  # ----------------------------------------------------------------------------
  #                        Internal helpers (private)
  # ----------------------------------------------------------------------------

  method _pad(item, desiredLen) -> rv
    if typeof(item) is 'undefined'
      # Undefined (probably unused) operand.
      # Show nothing.
      item = '-'
    endif

    if typeof(item) is 'array'
      # Operand is an array.
      # Render item-by-item.
      # TODO: Handle complex data in built-in str().
      rv  = '['
      sep = ''

      for idx in 0 .. len(item)
        rv  = rv + sep + str(item[idx])
        sep = ', '
      endfor
      rv = rv + ']'

    else
      # Default way.
      rv      = str(item)
      padCnt  = desiredLen - len(rv)

      for idx in 0 .. padCnt
        rv = rv + ' '
      endfor
    endif
  endmethod

  # ----------------------------------------------------------------------------
  #                                  Public API
  # ----------------------------------------------------------------------------

  method processOneItem(item)
    cnt             = len(this.quads)
    this.quads[cnt] = item
  endmethod

  method getQuads() -> rv
    rv = this.quads
  endmethod

  method _printSymbolInfoRow(name, readHits, writeHits, type, kind)
    name      = this._pad(name, 10)
    readHits  = this._pad(readHits, 4)
    writeHits = this._pad(writeHits, 4)
    type      = this._pad(type, 9)
    kind      = this._pad(kind, 8)
    print '    ;', name, readHits, writeHits, type, kind
  endmethod

  method _dumpFrameInfo(frameInfo)
    global SYMBOL_KIND_AS_TEXT

    symbolsIndex = frameInfo['_index']
    symbolsCnt   = len(symbolsIndex)

    this._printSymbolInfoRow('var', 'W', 'R', 'data type', 'kind')
    this._printSymbolInfoRow('----------', '----', '----', '---------', '--------')

    for idx in 0 .. symbolsCnt
      item = symbolsIndex[idx]
      this._printSymbolInfoRow(item.name, item.writeHits, item.readHits, item.type, SYMBOL_KIND_AS_TEXT[item.kind])
    endfor
    print ''
  endmethod

  method _getType(x) -> rv
    xInfo = this.currentFrame.symbols[x]
    if typeof(xInfo) is 'undefined'
      die('error: symbol not found: ' + str(x))
    else
      rv = this.currentFrame.symbols[x].type
      if typeof(rv) is 'undefined'
        rv = 'variant'
      endif
    endif
  endmethod

  method printQuads()
    code    = this.quads
    codeCnt = len(code)

    for idx in 0 .. codeCnt
      item    = code[idx]
      opcode  = item[0]

      # print '[ MIRQuadCollector ]', item

      if opcode is 'proc'
        print 'proc', item[1]['name']
        this._dumpFrameInfo(item[1]['symbols'])
        this.currentFrame = item[1]

      elif opcode is 'init_frame'
        print '    init_frame'

      elif opcode is 'endp'
        print 'endp'

      elif opcode is 'comment'
        print ';', item[1]

      elif opcode is 'label'
        print item[1] + ':'

      elif opcode is 'print_array_of_variants_ln'
        print '   ', this._pad(opcode, 28), item[1]

      elif opcode is 'call'
        print '   ', this._pad(opcode, 28), this._pad(item[1], 8), this._pad(item[2], 8), item[3]

      else
        dst = item[1]
        x   = item[2]
        y   = item[3]

        #if (opcode is 'move')# or (opcode is 'conv')
        #  dstType = this._getType(dst)
        #  srcType = this._getType(x)

        #  if dstType isnt srcType
          #  opcode = opcode + '_' + srcType
          #else
          #  opcode = 'conv_' + srcType + '_to_' + dstType
        #  endif
        #else

        if opcode[0] isnt 'c'
          if typeof(x) is 'string'
            xType  = this._getType(x)
            opcode = opcode + '_' + xType
          endif

          if typeof(y) is 'string'
            yType  = this._getType(y)
            if xType isnt yType
              opcode = opcode + '_' + yType
            endif
          endif
        endif

        paddedOpcode = this._pad(opcode, 28)
        paddedDst    = this._pad(dst, 8)
        paddedX      = this._pad(x, 8)
        paddedY      = this._pad(y, 8)

        print '   ', paddedOpcode, paddedDst, paddedX, paddedY
      endif
    endfor
  endmethod
endclass
