################################################################################
#                                                                              #
# This file is part of Mold project.                                           #
# Copyright (C) 2015, 2019 Sylwester Wysocki <sw143@wp.pl>                     #
#                                                                              #
# This program is free software: you can redistribute it and/or modify         #
# it under the terms of the GNU General Public License as published by         #
# the Free Software Foundation, either version 3 of the License, or            #
# (at your option) any later version.                                          #
#                                                                              #
# This program is distributed in the hope that it will be useful,              #
# but WITHOUT ANY WARRANTY; without even the implied warranty of               #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the                #
# GNU General Public License for more details.                                 #
#                                                                              #
# You should have received a copy of the GNU General Public License            #
# along with this program. If not, see <http://www.gnu.org/licenses/>          #
#                                                                              #
################################################################################

import 'AST/AST.mold'
import 'AST/ASTUtils.mold'

class ASTEvaluator
  method constructor(app)
    this.app = app
  endmethod

  method _debug(msg)
    global DEBUG_AST_OPTIMIZER
    if DEBUG_AST_OPTIMIZER is true
      print '[ ASTEvaluator ]', msg
    endif
  endmethod

  method _assignValue(valuesMap, lval, rval)
    name     = lval.value
    oldValue = valuesMap{name}
    newValue = rval

    valuesMap{name}  = newValue
    lval.storedValue = newValue
  endmethod

  method _getValue(valuesMap, name) -> rv
    rv = valuesMap{name}
    while (rv is undefined) and (valuesMap.parent is defined)
      valuesMap = valuesMap.parent
      rv        = valuesMap{name}
    endwhile
  endmethod

  method _mergeValues(x, y) -> rv
    rv = []

    # TODO: Optimize it.
    # Merge in first array if any.
    if x is defined
      if typeof(x) is 'array'
        for values item in x
          rv[len(rv)] = item
        endfor
      else
        rv[len(rv)] = x
      endif
    endif

    # Merge in second array if any.
    if y is defined
      if typeof(y) is 'array'
        for values item in y
          rv[len(rv)] = item
        endfor
      else
        rv[len(rv)] = y
      endif
    endif
  endmethod

  method _processConditionalNode(valuesMap, node)
    newValuesMap  = {'__parent': valuesMap}
    alreadyMerged = {}

    this._processNode(newValuesMap, node)

    for keys varId in newValuesMap
      # TODO: Remove this hack:
      if varId isnt '__parent'
        oldValue         = this._getValue(valuesMap, varId)
        newValue         = newValuesMap{varId}
        valuesMap{varId} = this._mergeValues(oldValue, newValue)
      endif
    endfor
  endmethod

  method _processTwoAlternativeNodes(valuesMap, node1, node2)
    newValuesMap1 = {'__parent': valuesMap}
    newValuesMap2 = {'__parent': valuesMap}

    this._processNode(newValuesMap1, node1)
    this._processNode(newValuesMap2, node2)

    for keys varId in newValuesMap1
      # TODO: Remove this hack.
      if varId isnt '__parent'
        oldValue         = this._getValue(valuesMap, varId)
        value1           = newValuesMap1{varId}
        value2           = newValuesMap2{varId}
        valuesMap{varId} = this._mergeValues(value1, value2)
      endif
    endfor

    for keys varId in newValuesMap2
      # TODO: Remove this hack.
      if varId isnt '__parent'
        oldValue         = this._getValue(valuesMap, varId)
        value1           = newValuesMap1{varId}
        value2           = newValuesMap2{varId}
        valuesMap{varId} = this._mergeValues(value1, value2)
      endif
    endfor
  endmethod

  method _getArrayOfValueNodes(node) -> rv
    storedValue = node.storedValue
    rv = []

    if storedValue is undefined
      # No values collected for this node yet.
      # Nothing to do.

    elif typeof(storedValue) is 'array'
      # There are more than one possible values for this node.
      # Node values is code flow dependent.
      for values item in storedValue
        rv[len(rv)] = item.id
      endfor

    else
      # There is exacly one possible node value.
      rv = [storedValue.id]
    endif
  endmethod

  method _processNode(valuesMap, node)
    global AST_OPCODE_PROGRAM
    global AST_OPCODE_FUNCTION
    global AST_OPCODE_CLASS_METHOD
    global AST_OPCODE_BLOCK
    global AST_OPCODE_VARIABLE
    global AST_OPCODE_IF
    global AST_OPCODE_DECLARE_OR_ASSIGN
    global AST_OPCODE_CALL
    global AST_OPCODE_CALL_NEW
    global AST_OPCODE_FOR_VALUES_RANGE
    global AST_OPCODE_FOR_VALUES
    global AST_OPCODE_FOR_KEYS
    global AST_OPCODE_FOR_INDEXES
    global AST_OPCODE_FOR_KEYS_VALUES
    global AST_OPCODE_FOR_INDEXES_VALUES
    global AST_OPCODE_WHILE
    global AST_OPCODE_NEG
    global AST_OPCODE_NOP
    global AST_OPCODE_COMPARE_EQ, AST_OPCODE_COMPARE_NEQ
    global AST_OPCODE_COMPARE_LT, AST_OPCODE_COMPARE_LE
    global AST_OPCODE_COMPARE_GT, AST_OPCODE_COMPARE_GE
    global AST_OPCODE_BOOL_NOT
    global AST_OPCODE_ADD, AST_OPCODE_SUB, AST_OPCODE_MUL, AST_OPCODE_DIV, AST_OPCODE_IDIV
    global AST_DATA_BOOL, AST_DATA_INTEGER, AST_DATA_DOUBLE, AST_DATA_STRING
    global AST_DATA_ARRAY, AST_DATA_MAP, AST_DATA_UNDEFINED, AST_DATA_OBJECT
    global AST_OPCODE_BOOL_AND, AST_OPCODE_BOOL_OR
    global AST_OPCODE_REFERENCE_BY_INDEX
    global AST_OPCODE_REFERENCE_BY_KEY
    global AST_OPCODE_CALL, AST_OPCODE_VCALL
    global SYMBOL_KIND_VAR_GLOBAL
    global AST_OPCODE_TYPE_CAST

    # TODO: Use null if ready?
    opcode      = node.opcode
    value       = node.value
    children    = node.children
    childrenCnt = len(children)

    #
    # Evaluate value.
    #

    if opcode is AST_OPCODE_VARIABLE
      symbolInfo = node.symbolInfo

      if ((symbolInfo is defined) and
          (symbolInfo.kind is SYMBOL_KIND_VAR_GLOBAL) and
          (symbolInfo.writeHits > 1))

        # Non-constant global. Treat as unique.
        node.storedValue = node

      else
        node.storedValue = this._getValue(valuesMap, node.value)
        # print "GET VALUE at node #" + str(node.id), node.value, this._getArrayOfValueNodes(node)
      endif

    elif opcode is AST_OPCODE_DECLARE_OR_ASSIGN
      lval = children[0]
      rval = children[1]

      this._processNode(valuesMap, rval)

      if rval.opcode is AST_OPCODE_VCALL
        # x = obj.foo()
        symbolInfo = rval.children[0].symbolInfo

        if symbolInfo is defined
          classProto = symbolInfo.classProto

          if classProto is defined
            methodProto = classProto.vtable{rval.value}

            if methodProto is defined
              if methodProto.rv is undefined
                die('error: method "' + classProto.name + '.' + methodProto.name + '" has no return value')
              endif
            endif
          endif
        endif

      elif rval.opcode is AST_OPCODE_VCALL
        # x = obj.foo()
        symbolInfo = rval.children[0].symbolInfo

        if symbolInfo is defined
          classProto = symbolInfo.classProto

          if classProto is defined
            methodProto = classProto.vtable{rval.value}

            if methodProto is defined
              if methodProto.rv is undefined
                die('error: method "' + classProto.name + '.' + methodProto.name + '" has no return value')
              endif
            endif
          endif
        endif
      endif

      if lval.opcode is AST_OPCODE_VARIABLE
        this._assignValue(valuesMap, lval, rval)

      elif ((lval.opcode is AST_OPCODE_REFERENCE_BY_INDEX) or
            (lval.opcode is AST_OPCODE_REFERENCE_BY_KEY))
        # TODO: Review it.
        if AST_IsOkAsBox(lval.children[0]) is false
          # Error - lval should stores variable name.
          # box[i] = ...
          die('error: array or map expected')
        endif

        this._assignValue(valuesMap, lval, lval)
        this._processNode(valuesMap, lval)
      endif

    elif opcode is AST_OPCODE_IF
      this._processNode(valuesMap, children[0])

      if childrenCnt is 2
        this._processConditionalNode(valuesMap, children[1])
      else
        this._processTwoAlternativeNodes(valuesMap, children[1], children[2])
      endif

    elif opcode is AST_OPCODE_FOR_VALUES_RANGE
      # for idx in valueStart .. valueEnd by step valueStep
      iter           = children[0]
      iterBeginValue = children[1]
      body           = children[4]

      this._assignValue(valuesMap, iter, iter)
      iter.storedValue = [iterBeginValue, iter]

      # First loop rotate.
      this._processConditionalNode(valuesMap, body)

      # N-th loop rotate.
      this._processConditionalNode(valuesMap, body)

    elif ((opcode is AST_OPCODE_FOR_VALUES) or
          (opcode is AST_OPCODE_FOR_KEYS) or
          (opcode is AST_OPCODE_FOR_INDEXES))

      # TODO: Review it.
      # for values v in box
      body = children[2]

      # First loop rotate.
      this._processConditionalNode(valuesMap, body)
      # N-th loop rotate.
      this._processConditionalNode(valuesMap, body)

    elif ((opcode is AST_OPCODE_FOR_KEYS_VALUES) or
          (opcode is AST_OPCODE_FOR_INDEXES_VALUES))

      # TODO: Review it.
      # for values v in box
      body = children[3]

      # First loop rotate.
      this._processConditionalNode(valuesMap, body)
      # N-th loop rotate.
      this._processConditionalNode(valuesMap, body)

    elif opcode is AST_OPCODE_WHILE
      cond = children[0]
      body = children[1]

      # First loop rotate.
      this._processNode(valuesMap, cond)
      this._processConditionalNode(valuesMap, body)

      # N-th loop rotate.
      this._processConditionalNode(valuesMap, cond)
      this._processConditionalNode(valuesMap, body)

    elif (opcode is AST_OPCODE_CALL) or (opcode is AST_OPCODE_VCALL)
      # foo(a, b, c, ...)
      # obj.foo(a,b, c, ...)
      # Parameter may be changed inside call.
      # TODO: Track real in/out parameters intent.
      for values childNode in children
        if childNode.opcode is AST_OPCODE_VARIABLE
          symbolInfo = childNode.symbolInfo

          # TODO: Review it.
          # global(x) gives undefined symbolInfo.
          if symbolInfo is defined
            astType = symbolInfo.astType

            if ((astType is undefined) or
                (astType is -1) or
                (astType is AST_DATA_OBJECT) or
                (astType is AST_DATA_MAP) or
                (astType is AST_DATA_ARRAY))

              # Unknown or box like variable is passed as call parameter.
              # We assume that it may be changed inside function.
              this._assignValue(valuesMap, childNode, childNode)
            endif
          endif
        endif

        this._processNode(valuesMap, childNode)
      endfor

    else
      if (opcode is AST_OPCODE_FUNCTION) or (opcode is AST_OPCODE_CLASS_METHOD)
        valuesMap = {}
      endif

      # Process children one-by-one recursively.
      for values childNode in children
        this._processNode(valuesMap, childNode)
      endfor
    endif
  endmethod

  method processNode(node)
    valuesMap = {}
    this._processNode(valuesMap, node)
  endmethod
endclass
