################################################################################
#                                                                              #
# This file is part of Mold project.                                           #
# Copyright (c) 2015, 2018 Sylwester Wysocki (sw143@wp.pl).                    #
#                                                                              #
# The Mold code and any derived work however based on this software are        #
# copyright of Sylwester Wysocki. Redistribution and use of the present        #
# software is allowed according to terms specified in the file LICENSE         #
# which comes in the source distribution.                                      #
#                                                                              #
# All rights reserved.                                                         #
#                                                                              #
################################################################################

import 'AST/ASTUtils.mold'

SYMBOL_KIND_VAR_LOCAL  = 1
SYMBOL_KIND_VAR_GLOBAL = 2
SYMBOL_KIND_VAR_PARAM  = 3
SYMBOL_KIND_VAR_RETVAL = 4
SYMBOL_KIND_CONST      = 5
SYMBOL_KIND_LABEL      = 6

SYMBOL_KIND_FUNCTION   = 7
SYMBOL_KIND_CLASS      = 8
SYMBOL_KIND_METHOD     = 9

SYMBOL_KIND_AS_TEXT = [
  '[error]',
  'local',
  'global',
  'param',
  'rv',
  'const',
  'label',
  'function',
  'class',
  'method'
]

SymbolTable_NextId = 0

function SymbolTable_GetNextId() -> rv
  global SymbolTable_NextId
  rv = SymbolTable_NextId
  SymbolTable_NextId = rv + 1
endfunction

class SymbolTable
  method constructor(app)
    this._debug('Created new object')
    this.app       = app
    this.deepIdx   = 0
    this.deepMax   = 128
    this.loopLevel = 0
    this.procLevel = 0

    # TODO: Use null.
    this.parent = false
    this.global = this

    # TODO: Remove it when map iteration ready.
    this.symbols = {'_index': []}
  endmethod

  method _debug(msg)
    if false
      print '[ SymbolTable ]', msg
    endif
  endmethod

  # ----------------------------------------------------------------------------
  #                               Generic calls
  # ----------------------------------------------------------------------------

  method getDeep() -> rv
    rv = this.deepIdx
  endmethod

  method exportSymbols() -> rv
    rv = this.symbols
  endmethod

  method insert(name, entry)
    if typeof(this.symbols[name]) isnt 'undefined'
      die("error: redeclaration of '" + name + "'")
    else
      # TODO: Remove it when map iteration ready.
      cnt = len(this.symbols['_index'])
      this.symbols['_index'][cnt] = entry

      this.symbols[name] = entry
      entry.writeHits = 0
      entry.readHits  = 0
      entry.loopLevel = this.loopLevel

      # Default data type to variant if not set.
      # TODO: Review it?
      #if typeof(entry.astType) is 'undefined'
      #  entry.astType = -1
      #endif
    endif
  endmethod

  method remove(name)
    # TODO: Optimize it.
    x = []
    entry = this.lookup(name)
    this.symbols[name] = x[0]

    symbolsIndex = this.symbols._index

    newIndex = []

    for idx in 0 .. len(symbolsIndex)
      if symbolsIndex[idx].name isnt entry.name
        newIndex[len(newIndex)] = symbolsIndex[idx]
      endif
    endfor

    this.symbols._index = newIndex
  endmethod

  method importExternalVariable(name)
    global SYMBOL_KIND_VAR_GLOBAL
    this.insert(name, {
      'name': name,
      'kind': SYMBOL_KIND_VAR_GLOBAL
    })
  endmethod

  method insertGlobal(name, entry)
    this.app.getSymbolTable().insert(name, entry)
  endmethod

  method _lookupInternal(name) -> rv
    global SYMBOL_KIND_VAR_LOCAL
    global SYMBOL_KIND_VAR_GLOBAL

    rv  = this.symbols[name]
    tab = this

    while (typeof(rv) is 'undefined') and (tab.deepIdx > 0)
      tab = tab.parent
      rv  = tab._lookupInternal(name)
    endwhile

    # Handle unimported global.
    # TODO: Clean up this mess.
    if (typeof(rv) isnt 'undefined')
      if (tab.procLevel is 0) and (this.procLevel > 0)
        if (rv.kind is SYMBOL_KIND_VAR_LOCAL) or (rv.kind is SYMBOL_KIND_VAR_GLOBAL)
          x  = []
          rv = x[0]
        endif
      endif
    endif
  endmethod

  method lookup(name) -> rv
    rv = this._lookupInternal(name)

    if typeof(rv) is 'undefined'
      die("error: '" + name + "' was not declared in this scope")
    endif

    # TODO: Use max(x, y) if ready.
    if this.loopLevel > rv.loopLevel
      rv.loopLevel = this.loopLevel
    endif
  endmethod

  method lookupForRead(name) -> rv
    rv          = this.lookup(name)
    rv.readHits = rv.readHits + 1
  endmethod

  method lookupForCall(name) -> rv
    # TODO: Special call counter?
    rv          = this.lookup(name)
    rv.readHits = rv.readHits + 1
  endmethod

  method lookupForWrite(name) -> rv
    rv           = this.lookup(name)
    rv.writeHits = rv.writeHits + 1
  endmethod

  method lookupForReadAndWrite(name) -> rv
    rv           = this.lookup(name)
    rv.readHits  = rv.readHits  + 1
    rv.writeHits = rv.writeHits + 1
  endmethod

  method lookupForWriteOrInsert(name) -> rv
    if this.isset(name) is false
      this.insertLocal(name)
    endif
    rv = this.lookupForWrite(name)
  endmethod

  method lookupGlobal(name) -> rv
    rv = this.global.lookup(name)
  endmethod

  method decreaseReadHits(name)
    entry = this.lookup(name)
    entry.readHits = entry.readHits - 1
  endmethod

  method decreaseWriteHits(name)
    entry = this.lookup(name)
    entry.writeHits = entry.writeHits - 1
  endmethod

  method isset(name) -> rv
    rv = typeof(this._lookupInternal(name)) isnt 'undefined'
  endmethod

  # ----------------------------------------------------------------------------
  #                   Scopes (0=global, 1,2,3...=local)
  # ----------------------------------------------------------------------------

  method enterScope() -> rv
    this._debug('Enter scope')

    if this.deepIdx >= this.deepMax
      die('error: too deep (' + str(this.deepIdx) + ')')
    endif

    rv = new SymbolTable(this.app)
    rv.parent  = this
    rv.deepIdx = this.deepIdx + 1
    rv.global  = this.global
  endmethod

  method enterScopeForLoop() -> rv
    rv           = this.enterScope()
    rv.loopLevel = this.loopLevel + 1
  endmethod

  method enterScopeForProcedure() -> rv
    rv           = this.enterScope()
    rv.procLevel = this.procLevel + 1
  endmethod

  method leaveScope() -> rv
    this._debug('Leave scope')

    if this.deepIdx < 1
      die('error: unmatched scope leave')
    endif
    this.vacuum()
    rv = this.parent
  endmethod

  # ----------------------------------------------------------------------------
  #                              Function helpers
  # ----------------------------------------------------------------------------

  method insertFunction(name, proto)
    global SYMBOL_KIND_FUNCTION
    newEntry = {
      'name': name,
      'kind': SYMBOL_KIND_FUNCTION,
      'proto': proto
    }

    this.insertGlobal(name, newEntry)
  endmethod

  method lookupFunction(name, paramsCnt) -> rv
    global SYMBOL_KIND_FUNCTION
    rv = this.lookupGlobal(name)

    if rv['kind'] isnt SYMBOL_KIND_FUNCTION
      die("error: '" + name + "' is not a function")

    elif rv['proto']['paramsCnt'] isnt paramsCnt
      die("error: function '" + name + "' requires exactly " + str(rv['proto']['paramsCnt']) + ' parameters, but ' + str(paramsCnt) + ' were passed')
    endif
  endmethod

  # ----------------------------------------------------------------------------
  #                               Class helpers
  # ----------------------------------------------------------------------------

  method insertClass(name, proto)
    global SYMBOL_KIND_CLASS
    newEntry = {
      'name': name,
      'kind': SYMBOL_KIND_CLASS,
      'proto': proto
    }
    this.insertGlobal(name, newEntry)
  endmethod

  method lookupClass(name) -> rv
    global SYMBOL_KIND_CLASS
    rv = this.lookupGlobal(name)
    if rv['kind'] isnt SYMBOL_KIND_CLASS
      die("error: '" + name + "' is not a class")
    endif
  endmethod

  method lookupMethod(className, methodName, paramsCnt) -> rv
    rv = this.lookupClass(className)['proto']['vtable'][methodName]

    if typeof(rv) is 'undefined'
      die("error: class '" + className + "' has no method '" + methodName + "'")

    elif rv['paramsCnt'] isnt paramsCnt
      die("error: method '" + methodName + "' requires exactly " + str(rv['paramsCnt']) + ' parameters, but ' + str(paramsCnt) + ' were passed')
    endif
  endmethod

  # ----------------------------------------------------------------------------
  #                               Variable helper
  # ----------------------------------------------------------------------------

  method insertLocal(name)
    global SYMBOL_KIND_VAR_LOCAL

    if this.deepIdx < 1
      die('error: attemp to declare local within global table')
    endif

    newEntry = {
      'name': name,
      'kind': SYMBOL_KIND_VAR_LOCAL
    }
    this.insert(name, newEntry)
  endmethod

  method importGlobal(name)
    global SYMBOL_KIND_VAR_GLOBAL

    if this.deepIdx <= 1
      die('error: global directive used in global space')

    elif this.isset(name) is true
      # Symbol already declared in current scope.
      # Check is it global.
      symbolInfo = this.lookup(name)

      if symbolInfo.kind is SYMBOL_KIND_VAR_GLOBAL
        # It's already imported global. Do nothing.
      else
        # It's already declared as another kind.
        die("error: conflicting declaration 'global " + name + "'")
      endif

    else
      # TODO: Clean up this mess.
      mainScope = this
      while mainScope.deepIdx > 1
        mainScope = mainScope.parent
      endwhile

      # Find symbol in global scope.
      symbolInfo = mainScope.lookup(name)

      # Keep access stats after import.
      writeHits = symbolInfo.writeHits
      readHits  = symbolInfo.readHits
      loopLevel = this.loopLevel

      if symbolInfo.kind isnt SYMBOL_KIND_VAR_GLOBAL
        # Symbol found in main() function for first time.
        # Change to global and move to global symbol table.
        symbolInfo.kind = SYMBOL_KIND_VAR_GLOBAL

        # Move into global scope.
        this.global.insert(name, symbolInfo)
      endif

      # Import global var into current scope.
      this.insert(name, symbolInfo)

      # Keep access stats after import.
      symbolInfo.writeHits = writeHits
      symbolInfo.readHits  = readHits

      # TODO: Use max.
      if loopLevel > symbolInfo.loopLevel
        symbolInfo.loopLevel = loopLevel
      endif
    endif
  endmethod

  method insertParam(name)
    global SYMBOL_KIND_VAR_PARAM
    newEntry = {
      'name': name,
      'kind': SYMBOL_KIND_VAR_PARAM
    }
    this.insert(name, newEntry)
  endmethod

  method insertRetVal(name)
    global SYMBOL_KIND_VAR_RETVAL
    newEntry = {
      'name': '__rv',
      'kind': SYMBOL_KIND_VAR_RETVAL,
      'alias': name,
      'astType': -1
    }
    this.insert('__rv', newEntry)
    this.symbols[name] = newEntry
    this.lookupForRead('__rv')
  endmethod

  method insertConst(name, value)
    global SYMBOL_KIND_CONST
    newEntry = {
      'name': name,
      'kind': SYMBOL_KIND_CONST,
      'value': value
    }

    # TODO: Review it
    this.insert(name, newEntry)
    this.global.insert(name, newEntry)
  endmethod

  # ----------------------------------------------------------------------------
  #                               Constant helpers
  # ----------------------------------------------------------------------------

  method createConstFromValue(value) -> rv
    rv = '__const' + str(SymbolTable_GetNextId())
    this.insertConst(rv, value)
  endmethod

  method assignValue(name, value)
    symbolInfo = this.lookup(name)
    symbolInfo.value = value
  endmethod

  method assignNode(name, node)
    symbolInfo = this.lookup(name)
    symbolInfo.node = node
  endmethod

  method assignType(name, newType)
    global AST_DATA_INTEGER
    global AST_DATA_DOUBLE

    symbolInfo = this.lookup(name)
    oldType    = symbolInfo.astType

    if newType isnt oldType
      goOn = false

      if typeof(oldType) is 'undefined'
        # First type assign. Go on.
        goOn = true

      elif oldType is -1
        # Type is already variant. Don't touch it.

      elif (oldType is AST_DATA_INTEGER) and (newType is AST_DATA_DOUBLE)
        # integer -> double
        # Go on.
        goOn = true

      elif newType is -1
        # Variant overwrite all types. Go on.
        goOn = true

      else
        # Error - type mismatch.
        die('error: attemp to assign ' + AST_ConvertDataTypeToText(newType) + ' to ' + AST_ConvertDataTypeToText(oldType))
      endif

      if goOn is true
        symbolInfo.astType = newType
        this._debug('Assign AST type [' + AST_ConvertDataTypeToText(newType) + '] to variable [' + str(name) + ']')
      else
        this._debug('Ignored ineffective AST type change from [' + AST_ConvertDataTypeToText(oldType) + '] to [' + AST_ConvertDataTypeToText(newType) + '] for variable [' + str(name) + ']')
      endif
    endif
  endmethod

  method assignMirType(name, type)
    symbolInfo = this.lookup(name)
    symbolInfo.type = type
  endmethod

  method vacuum()
/*
    TODO: Review it.

    global SYMBOL_KIND_CONST

    symbolsIndex = this.symbols._index

    # TODO: Use remove() if ready.
    #this.symbols = {'_index': []}

    for idx in 0 .. len(symbolsIndex)
      symbolInfo = symbolsIndex[idx]
      if (symbolInfo.writeHits is 1) and typeof(symbolInfo.value) isnt 'undefined'
        # Variable written once by imm. Convert to constant.
        symbolInfo.kind = SYMBOL_KIND_CONST
      endif

      #if symbolInfo.writeHits > 0
      #  this.insert(symbolInfo.name, symbolInfo)
      #endif
    endfor
*/
  endmethod

  method removeUnusedSymbols()
    global SYMBOL_KIND_VAR_PARAM
    global SYMBOL_KIND_VAR_RETVAL

    index      = this.symbols._index
    indexCnt   = len(index)
    newIndex   = []
    newSymbols = {'_index': newIndex}

    # TODO: Use remove() if ready.
    #this.symbols = {'_index': []}

    for idx in 0 .. indexCnt
      symbolInfo = index[idx]
      if (symbolInfo.writeHits > 0) or (symbolInfo.kind is SYMBOL_KIND_VAR_PARAM) or (symbolInfo.kind is SYMBOL_KIND_VAR_RETVAL) or (symbolInfo.name is '__rv')
        # Variable is used at least once time. Keep it in table.
        newIndex[len(newIndex)]     = symbolInfo
        newSymbols[symbolInfo.name] = symbolInfo
      else
        # Variable was neither written nor read. Remove it from table.
        this._debug('Removed unused symbol [' + symbolInfo.name + ']')
      endif
    endfor

    this.symbols = newSymbols
  endmethod

  # ----------------------------------------------------------------------------
  #                               Label helpers
  # ----------------------------------------------------------------------------

  method createLabel() -> rv
    global SYMBOL_KIND_LABEL

    # TODO: Store labels in symbol table too.
    rv = 'l' + str(SymbolTable_GetNextId())
    this.insert(rv, {
      'name': rv,
      'kind': SYMBOL_KIND_LABEL
    })
  endmethod

  # ----------------------------------------------------------------------------
  #                        Temporary variable helpers
  # ----------------------------------------------------------------------------

  method createTempVar() -> rv
    rv = 'tmp' + str(SymbolTable_GetNextId())
    this.insertLocal(rv)
  endmethod

  method createTempVarForWrite() -> rv
    rv = this.createTempVar()
    this.lookupForWrite(rv)
  endmethod

  method createTempVarForReadAndWrite() -> rv
    rv = this.createTempVar()
    this.lookupForRead(rv)
    this.lookupForWrite(rv)
  endmethod
endclass
