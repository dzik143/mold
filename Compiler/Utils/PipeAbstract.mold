################################################################################
#                                                                              #
# This file is part of Mold project.                                           #
# Copyright (C) 2015, 2019 Sylwester Wysocki <sw143@wp.pl>                     #
#                                                                              #
# This program is free software: you can redistribute it and/or modify         #
# it under the terms of the GNU General Public License as published by         #
# the Free Software Foundation, either version 3 of the License, or            #
# (at your option) any later version.                                          #
#                                                                              #
# This program is distributed in the hope that it will be useful,              #
# but WITHOUT ANY WARRANTY; without even the implied warranty of               #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the                #
# GNU General Public License for more details.                                 #
#                                                                              #
# You should have received a copy of the GNU General Public License            #
# along with this program. If not, see <http://www.gnu.org/licenses/>          #
#                                                                              #
################################################################################

class PipeAbstract

  # ----------------------------------------------------------------------------
  #                         Init functions (constructor)
  # ----------------------------------------------------------------------------

  method constructor()
    this.outputObject     = false
    this.buffer           = []
    this.bufferingEnabled = false
  endmethod

  # ----------------------------------------------------------------------------
  #                     Internal implementation (private)
  # ----------------------------------------------------------------------------

  method _emitOneItem(item)
    if this.bufferingEnabled is true
      buffer      = this.buffer
      cnt         = len(buffer)
      buffer[cnt] = item
    else
      outputObject = this.outputObject
      outputObject.processOneItem(item)
    endif
  endmethod

  method _replaceLastItem(item)
    if this.bufferingEnabled is true
      buffer          = this.buffer
      cnt             = len(buffer)
      buffer[cnt - 1] = item
    else
      die('error: _replaceLastItem() called without buffering')
    endif
  endmethod

  # ----------------------------------------------------------------------------
  #                                 Public API
  # ----------------------------------------------------------------------------

  method connectTo(outputObject) -> rv
    this.outputObject = outputObject
    rv = outputObject
  endmethod

  method processOneItem(item)
  endmethod

  method processArrayOfItems(arrayOfItems)
    for values oneItem in arrayOfItems
      this.processOneItem(oneItem)
    endfor
  endmethod

  method flush()
    buffer       = this.buffer
    outputObject = this.outputObject

    for values oneItem in buffer
      outputObject.processOneItem(oneItem)
    endfor

    this.buffer = []
  endmethod
endclass
